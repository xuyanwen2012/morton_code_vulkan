#version 450

layout(local_size_x = 256) in;

layout(std430, binding = 0) buffer InputBuffer { vec3 input_xyz[]; };
layout(std430, binding = 1) buffer OutputBuffer { uint output_code[]; };

uint expand_bit(uint a) {
  uint x = a & 0x000003FF;
  x = (x | x << 16) & 0x030000FF;
  x = (x | x << 8) & 0x0300F00F;
  x = (x | x << 4) & 0x030C30C3;
  x = (x | x << 2) & 0x09249249;
  return x;
}

uint encode(uvec3 ijk) {
  return expand_bit(ijk.x) | expand_bit(ijk.y) << 1 | expand_bit(ijk.z) << 2;
}

const uint kCodeLen = 31;
const float min_coord = 0.0f;
const float range = 1024.0f;

void main() {
  uint index = gl_GlobalInvocationID.x;

  float x = input_xyz[index].x;
  float y = input_xyz[index].y;
  float z = input_xyz[index].z;

  // point to code
  const uint bit_scale = 0xFFFFFFFFu >> (32 - (kCodeLen / 3));
  uint i = uint(bit_scale * ((x - min_coord) / range));
  uint j = uint(bit_scale * ((y - min_coord) / range));
  uint k = uint(bit_scale * ((z - min_coord) / range));

  output_code[index] = encode(uvec3(i, j, k));
}
